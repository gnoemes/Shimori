import com.gnoemes.shimori.Application
import com.gnoemes.shimori.Libs
import com.gnoemes.shimori.Modules

apply plugin: 'com.android.application'
apply from: '../android_commons.gradle'
apply from: '../android_core_dependencies.gradle'

android {
    def version = readVersion(new File("${project.projectDir}/version.properties"))

    def versionMajor = version["major"] as int
    def versionMinor = version["minor"] as int
    def versionPatch = version["patch"] as int
    def versionBuild = version["build"] as int

    //TODO increment version on release builds
    def autoIncrementVersionCode = version["AI_VERSION_CODE"] as int

    def appVersionName = "$versionMajor.$versionMinor.$versionPatch"
    def appVersionCode = autoIncrementVersionCode

    println "config code: ${appVersionCode}, name: ${appVersionName}"

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Shimori-v${versionName}-${variant.buildType.name}"
        }
    }

    defaultConfig {
        applicationId Application.id
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        buildConfigField 'String', 'ShikimoriClientId', "\"" + propOrDef("ShikimoriClientId", "") + "\""
        buildConfigField 'String', 'ShikimoriClientSecret', "\"" + propOrDef("ShikimoriClientSecret", "") + "\""
        buildConfigField 'String', 'ShikimoriBaseUrl', ShikimoriBaseUrl
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(Modules.baseAndroid)
    implementation project(Modules.common)
    implementation project(Modules.navigation)
    implementation project(Modules.domain)
    implementation project(Modules.model)
    implementation project(Modules.shikimoriAuth)

    implementation project(Modules.featureSearch)
    implementation project(Modules.featureCalendar)
    implementation project(Modules.featureRates)

    implementation Libs.Dagger.androidSupport
    kapt Libs.Dagger.compiler
    kapt Libs.Dagger.androidProcessor

    compileOnly Libs.AssistedInject.annotationDagger2
    kapt Libs.AssistedInject.processorDagger2

    implementation Libs.Coroutines.core
    implementation Libs.Coroutines.android

    implementation Libs.Epoxy.epoxy
    implementation Libs.Epoxy.paging
    implementation Libs.Epoxy.dataBinding
    kapt Libs.Epoxy.processor

    implementation Libs.gson
    implementation Libs.Retrofit.gsonConverter

    implementation Libs.mvRx

    implementation Libs.jodaTime

    implementation Libs.Glide.glide
    kapt Libs.Glide.compiler


    testImplementation Libs.junit
    testImplementation Libs.robolectric

    debugImplementation Libs.Debug.leakCanary
    debugImplementation Libs.Debug.dbDebug
}
