import com.gnoemes.shimori.Modules

plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'

    id 'kotlin-android'
    id 'kotlin-kapt'
}
apply from: '../android_commons.gradle'
apply from: '../android_core_dependencies.gradle'

android {
    def version = readVersion(new File("${project.projectDir}/version.properties"))

    def versionMajor = version["major"] as int
    def versionMinor = version["minor"] as int
    def versionPatch = version["patch"] as int
    def versionBuild = version["build"] as int

    //TODO increment version on release builds
    def autoIncrementVersionCode = version["AI_VERSION_CODE"] as int

    def appVersionName = "$versionMajor.$versionMinor.$versionPatch"
    def appVersionCode = autoIncrementVersionCode

    println "config code: ${appVersionCode}, name: ${appVersionName}"

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Shimori-v${versionName}-${variant.buildType.name}.apk"
        }
    }

    defaultConfig {
        applicationId com.gnoemes.shimori.Application.id
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'ShikimoriClientId', "\"" + propOrDef("ShikimoriClientId", "") + "\""
        buildConfigField 'String', 'ShikimoriClientSecret', "\"" + propOrDef("ShikimoriClientSecret", "") + "\""
        buildConfigField 'String', 'ShikimoriBaseUrl', ShikimoriBaseUrl
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(Modules.baseAndroid)
    implementation project(Modules.common)
    implementation project(Modules.domain)
    implementation project(Modules.model)
    implementation project(Modules.shikimoriAuth)

    implementation libs.google.crashlytics
    implementation libs.google.analytics

    implementation libs.gson

    debugImplementation libs.leakCanary

    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.rules
}
